Sub Stocks()

    Dim ws As Worksheet
    Dim Total As Double
    Dim LastRow As Long
    Dim LastResultsRow As Long
    Dim i As Long
    Dim j As Long
    Dim k As Long
    Dim OpenPrice As Double
    Dim ClosePrice As Double
    
    For Each ws In ActiveWorkbook.Worksheets ' Loop through all workbooks
    
        ' Write column headers
        ws.Cells(1, 9) = "Ticker"
        ws.Cells(1, 10) = "Yearly Change"
        ws.Cells(1, 11) = "Percent Change"
        ws.Cells(1, 12) = "Total Stock Volume"
        
        j = 2 ' Start results in second row
        LastRow = ws.Cells(Rows.Count, 1).End(xlUp).Row
        
        ' Find open price for first stock on sheet
        OpenPrice = ws.Cells(2, 3).Value
        For i = 2 To LastRow
            If ws.Cells(i + 1, 1).Value = ws.Cells(i, 1).Value Then
                ' Add total volume if ticker is the same
                Total = Total + ws.Cells(i, 7).Value
            Else
                ' Add final stock volume
                Total = Total + ws.Cells(i, 7).Value
                ' Find closing price
                ClosePrice = ws.Cells(i, 6).Value
                ' Write ticker
                ws.Cells(j, 9).Value = ws.Cells(i, 1).Value
                ' Calculate yearly change
                ws.Cells(j, 10).Value = ClosePrice - OpenPrice
                ' Calculate percentage of yearly change
                ws.Cells(j, 11).Value = (ws.Cells(j, 10).Value / OpenPrice) ' * 100
                ' Write total stock volume
                ws.Cells(j, 12).Value = Total
                Total = 0 ' Reset total stock volume
                j = j + 1 ' Move one row down for next ticker
                ' Find new opening price
                OpenPrice = ws.Cells(i + 1, 3).Value
            End If
        Next i
        
        ' Write labels for greatest % changes and total volume
        ws.Cells(1, 16) = "Ticker"
        ws.Cells(1, 17) = "Value"
        ws.Cells(2, 15) = "Greatest % Increase"
        ws.Cells(3, 15) = "Greatest % Decrease"
        ws.Cells(4, 15) = "Greatest Total Volume"
        
        ' Find greatest % changes and total volume + associated tickers
        ws.Cells(2, 17).Value = 0
        ws.Cells(3, 17).Value = 0
        ws.Cells(4, 17).Value = 0
        LastResultsRow = ws.Cells(Rows.Count, 9).End(xlUp).Row
        For k = 2 To LastResultsRow
            ' Replace greatest % increase/decrease if next row is larger
            If ws.Cells(k, 11).Value > ws.Cells(2, 17).Value Then
                ws.Cells(2, 16) = ws.Cells(k, 9)
                ws.Cells(2, 17).Value = ws.Cells(k, 11).Value
            ElseIf ws.Cells(k, 11).Value < ws.Cells(3, 17).Value Then
                ws.Cells(3, 16) = ws.Cells(k, 9)
                ws.Cells(3, 17).Value = ws.Cells(k, 11).Value
            Else: End If
            ' Replace greatest volume if next row is larger
            If ws.Cells(k, 12).Value > ws.Cells(4, 17).Value Then
                ws.Cells(4, 16) = ws.Cells(k, 9)
                ws.Cells(4, 17).Value = ws.Cells(k, 12).Value
            Else: End If
        Next k
        ' Add conditional formatting by hand
    Next ws
End Sub

